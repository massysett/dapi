Here is a grammar for various productions used throughout
dapi. Terminals are double quoted. All terminals are case insensitive.


The range is a key start symbol in dapi. Each of the non-option
arguments is a range.

range ::= moddedUnit | monthYear | absYear | datedRange

monthYear ::= monthAbbrev absYear

datedRange ::= dateSpec rangeSep dateSpec

rangeSep ::= "-"

For moddedUnit, no mod is the same as "this".

moddedUnit ::= mod? unitOrMonth

unitOrMonth ::= unit | monthAbbrev

unit ::= "day" | "week" | "month" | "year" | "decade" | "century" |
"millennium" | "quarter"

mod ::= modText | modArith

modText ::= "this" | "next" | "last"

modArith ::= sign digits

digits ::= digit+

digit ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

sign ::= "+" | "-"

dateSpec ::= absolute | relative

absolute ::= absYear sep month sep dayOrLast

absYear ::= digits

sep ::= "/" | "-"

* Months

month ::= digits | monthAbbrev

monthAbbrev ::= "jan" | "feb" | "mar" | "apr" | "may" | "jun" | "jul"
| "aug" | "sep" | "oct" | "nov" | "dec"

* Day

Last indicates the last day of that month.

day ::= digits

dayOrLast ::= digits | last

last ::= "l"

* Relative

relative ::= relDay | relDayOfWeek | relByUnit

For relDayOfWeek, no mod is the same as "this". "this" picks the day
that is in the current week, whether in the past or future. "next"
from next week, "last" from last week. +1 is the next occurring day,
excluding today. -1 is the previous occurring day, excluding today.

relDayOfWeek ::= mod? dayOfWeek

relDay ::= "today" | "yesterday" | "tomorrow"

relByUnit ::= modArith unit

* Day of week

dayOfWeek ::= dowName

dowName ::= "sun" | "mon" | "tue" | "wed" | "thu" | "fri" | "sat"
